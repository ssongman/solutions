

# 1. Service Principal 설정

먼저, Azure 환경에 접근하려면 해당 구독에 대한 Service Principal을 생성해야 한다. Azure CLI를 사용하여 쉽게 생성할 수 있다.



## 1) SP 생성



### (1) Service Principal 생성

```sh
# subscriptions 확인
$ az account list -o table
Name         CloudName    SubscriptionId                        TenantId                              State    IsDefault
-----------  -----------  ------------------------------------  ------------------------------------  -------  -----------
axcoe        AzureCloud   1d6c45e0-bd9f-4771-bb67-36616452f239  a3870566-f011-4691-aa51-5b988c51c03a  Enabled  False

Subscription_ID="1d6c45e0-bd9f-4771-bb67-36616452f239"


# SP 생성
$ az ad sp create-for-rbac \
    -n yjApp \
    --role="Contributor" \
    --scopes="/subscriptions/1d6c45e0-bd9f-4771-bb67-36616452f239"


Creating 'Contributor' role assignment under scope '/subscriptions/1d6c45e0-bd9f-4771-bb67-36616452f239'
The output includes credentials that you must protect. Be sure that you do not include these credentials in your code or check the credentials into your source control. For more information, see https://aka.ms/azadsp-cli
{
  "id": "22027e34-d6c2-49bb-8e2a-78a965b338a6",
  "appId": "....",
  "displayName": "azure-cli-2024-09-21-08-19-38",
  "password": "....",
  "tenant": "...."
}
{
  "appId": "37b7bc6f-b65f-4435-a9ba-37c580f19fca",
  "displayName": "yjApp",
  "password": "......,
  "tenant": "a3870566-f011-4691-aa51-5b988c51c03a"
}
# password 는 최초 한번만 식별 가능


# 확인
$ az ad sp show --id 22027e34-d6c2-49bb-8e2a-78a965b338a6
  
$ az ad sp list -o table
$ az ad sp list --display-name azure-cli-2024-09-21-08-19-38 
$ az ad sp list --display-name azure-cli-2024-09-21-08-19-38 -o table




# 삭제
$ az ad sp delete --id 22027e34-d6c2-49bb-8e2a-78a965b338a6
$ az ad sp delete --id 37b7bc6f-b65f-4435-a9ba-37c580f19fca



```





### (3) 환경변수 설정

```sh
export ARM_CLIENT_ID="<APPID_VALUE>"
export ARM_CLIENT_SECRET="<PASSWORD_VALUE>"
export ARM_SUBSCRIPTION_ID="<SUBSCRIPTION_ID>"
export ARM_TENANT_ID="<TENANT_VALUE>"

```







# 2. yj 용 VM 생성 





## 1) directory 생성

```sh
$ mkdir -p ~/song/terraform/yjVM
  cd ~/song/terraform/yjVM

```





## 2) TF 파일 생성



### (1) VM에 설치할 Tools 스크립트



```sh
$ cat > install_tools.sh
---
#!/bin/bash
sudo apt-get update
sudo apt-get install -y docker.io
sudo systemctl start docker
sudo systemctl enable docker
sudo snap install k9s
curl https://raw.githubusercontent.com/helm/helm/master/scripts/get-helm-3 | bash
---


sudo apt install vim -y
sudo apt install tree
sudo apt install iputils-ping
sudo apt install net-tools
sudo apt install netcat
sudo apt install unzip
sudo apt install git

```



### (2) VM 1개

```sh

$ mkdir -p ~/song/terraform/yjVM/singleVm
  cd ~/song/terraform/yjVM/singleVm
  
```



#### variable.tf

```sh
$ cat > variables.tf
```

```python
# variables.tf
variable "admin_username" {
  description = "Admin username for the VM"
  type        = string
}

variable "admin_password" {
  description = "Admin password for the VM"
  type        = string
  sensitive   = true
}

```



#### main.tf

```sh
$ cat > main.tf
```



```python
provider "azurerm" {
  features {}
}

# Resource Group 생성
resource "azurerm_resource_group" "rg" {
  name     = "yj-rg"
  location = "koreacentral"
}

# Virtual Network 생성
resource "azurerm_virtual_network" "vnet" {
  name                = "eduVnet"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

# Subnet 생성
resource "azurerm_subnet" "subnet" {
  name                 = "eduSubnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.1.0/24"]
}

# Network Security Group
resource "azurerm_network_security_group" "nsg" {
  name                = "eduNSG"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

# NSG inbound rule(SSH port 22)
resource "azurerm_network_security_rule" "ssh" {
  name                        = "SSH"
  priority                    = 1001
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "22"
  source_address_prefix       = "*"
  destination_address_prefix  = "*"
  network_security_group_name = azurerm_network_security_group.nsg.name
  resource_group_name         = azurerm_resource_group.rg.name
}


# Network Interface
resource "azurerm_network_interface" "nic" {
  name                = "eduNIC"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  ip_configuration {
    name                          = "eduNICConfiguration"
    subnet_id                     = azurerm_subnet.subnet.id
    private_ip_address_allocation = "Dynamic"
    
    # public IP Connect
    public_ip_address_id = azurerm_public_ip.pip.id
  }
}


# NSG NIC connect
resource "azurerm_network_interface_security_group_association" "nic_nsg" {
  network_interface_id      = azurerm_network_interface.nic.id
  network_security_group_id = azurerm_network_security_group.nsg.id
}


# Public IP
resource "azurerm_public_ip" "pip" {
  name                = "eduPublicIP"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  allocation_method   = "Static"   # Static IP 할당으로 변경
  sku                 = "Standard"
}

# VM
resource "azurerm_virtual_machine" "vm" {
  name                  = "eduVM"
  location              = azurerm_resource_group.rg.location
  resource_group_name   = azurerm_resource_group.rg.name
  network_interface_ids = [azurerm_network_interface.nic.id]
  vm_size               = "Standard_DS1_v2"

  # Ubuntu Image 사용
  storage_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts-gen2"
    version   = "latest"
  }

  # OS 디스크 설정
  storage_os_disk {
    name              = "eduOSDisk"
    caching           = "ReadWrite"
    create_option     = "FromImage"
    managed_disk_type = "Standard_LRS"
  }

  # 관리자 계정 설정
  os_profile {
    computer_name  = "eduVM"
    admin_username = var.admin_username
    admin_password = var.admin_password
  }

  # SSH 설정
  os_profile_linux_config {
    disable_password_authentication = false
  }
  
  # upload file to VM
  provisioner "file" {
    source      = "../install_tools.sh"
    destination = "/tmp/install_tools.sh"

    connection {
      type     = "ssh"
      user     = var.admin_username
      password = var.admin_password
      host     = azurerm_public_ip.pip.ip_address
      port     = 22
    }
  }

  # exec uploaded script
  provisioner "remote-exec" {
    inline = [
      "chmod +x /tmp/install_tools.sh",
      "sudo /tmp/install_tools.sh"
    ]

    connection {
      type     = "ssh"
      user     = var.admin_username
      password = var.admin_password
      host     = azurerm_public_ip.pip.ip_address
      port     = 22
    }
  }

  # 퍼블릭 IP
  depends_on = [azurerm_public_ip.pip]
}

# Output: Public IP
output "public_ip" {
  value = azurerm_public_ip.pip.ip_address
}
```







## 3) **Terraform 단계별 실행**



### (1) azure env 설정

```sh
export ARM_CLIENT_ID="<APPID_VALUE>"
export ARM_CLIENT_SECRET="<PASSWORD_VALUE>"
export ARM_SUBSCRIPTION_ID="<SUBSCRIPTION_ID>"
export ARM_TENANT_ID="<TENANT_VALUE>"

export TF_VAR_admin_username="<vm_admin_username>"
export TF_VAR_admin_password="<vm_admin_password>"

===============================

# 20241020
export ARM_CLIENT_ID="37b7bc6f-b65f-4435-a9ba-37c580f19fca"
export ARM_CLIENT_SECRET="yap8Q~41~..."
export ARM_SUBSCRIPTION_ID="1d6c45e0-bd9f-4771-bb67-36616452f239"
export ARM_TENANT_ID="a3870566-f011-4691-aa51-5b988c51c03a"

export TF_VAR_admin_username="song"
export TF_VAR_admin_password="Songpass123!"

```



### (2) 실행

```bash
terraform init

terraform validate

terraform plan

terraform apply
 

terraform show

```



### (3) trouble shooting

```sh
# 유효한 image 목록 확인
$ az vm image list --output table

```





# 3. yj 용 VMs 생성 



## 1) directory 생성

```sh

$ mkdir -p ~/song/terraform/yjVMs
  cd ~/song/terraform/yjVMs

```





## 2) TF 파일 생성



### (1) VM에 설치할 Tools 스크립트

#### tools

```sh

$ cat > install_tools.sh
```



```sh
#!/bin/bash

# upadte
sudo apt-get update


# etc
sudo apt install vim -y
sudo apt install tree
sudo apt install iputils-ping
sudo apt install net-tools
sudo apt install netcat
sudo apt install unzip
sudo apt install git

sudo apt install maven -y
sudo apt install tree
sudo apt install jq -y



```



#### [참고] k8s_tools 

```sh
$ cat > install_k8s_tools.sh
```

```sh
#!/bin/bash


# 0) K8s tools
TEMP_DIR="/tmp/install_k8s_tools"
INSTALL_DIR="/usr/local/bin"
mkdir -p $TEMP_DIR
cd $TEMP_DIR


# 1) k9s
echo "Install k9s..."
K9S_VERSION="v0.32.4"
ARCH="amd64"
TAR_FILE="k9s_Linux_${ARCH}.tar.gz"
DOWNLOAD_URL="https://github.com/derailed/k9s/releases/download/${K9S_VERSION}/${TAR_FILE}"

echo "Downloading k9s version ${K9S_VERSION}..."
curl -LO ${DOWNLOAD_URL}

echo "Extracting k9s..."
tar -xzf ${TAR_FILE}

echo "Installing k9s to ${INSTALL_DIR}..."
sudo mv k9s ${INSTALL_DIR}/k9s
sudo chmod +x ${INSTALL_DIR}/k9s
k9s version


# 2) kustomize tool
echo "Install kustomize..."
curl -s "https://raw.githubusercontent.com/kubernetes-sigs/kustomize/master/hack/install_kustomize.sh"  | bash
sudo mv kustomize ${INSTALL_DIR}/kustomize
sudo chmod +x ${INSTALL_DIR}/kustomize
kustomize version


# 3) helm & azure cli tool
curl https://raw.githubusercontent.com/helm/helm/master/scripts/get-helm-3 | sudo bash
curl -sL https://aka.ms/InstallAzureCLIDeb | sudo bash


# 4) Docker
sudo apt-get install -y apt-transport-https ca-certificates curl gnupg-agent software-properties-common
curl -fsSL https://download.docker.com/linux/ubuntu/gpg | sudo apt-key add -
sudo add-apt-repository -y "deb [arch=amd64] https://download.docker.com/linux/ubuntu $(lsb_release -cs) stable"
sudo apt-get install -y docker-ce docker-ce-cli containerd.io
sudo systemctl status docker --no-pager
sudo usermod -aG docker ktdseduuser
sudo service docker restart



# 9) TEMP_DIR Cleaning
echo "Cleaning up..."
rm -rf $TEMP_DIR


```





### (3) 대량 VM 생성

```sh

$ mkdir -p ~/song/terraform/yjVMs/multiVm
  cd ~/song/terraform/yjVMs/multiVM

```



#### variable.tf

```sh
$ cat > variables.tf
```

```python
# variables.tf
variable "admin_username" {
  description = "Admin username for the VM"
  type        = string
}

variable "admin_password" {
  description = "Admin password for the VM"
  type        = string
  sensitive   = true
}

variable "vm_cnt" {
  description = "Number of VMs to create"
  type        = number
  default     = 3
}


```



#### main.tf

```sh
$ cat > main.tf
```



```python
provider "azurerm" {
  features {}
}

# Resource Group 생성
resource "azurerm_resource_group" "rg" {
  name     = "yjvms-rg"
  location = "koreacentral"
}

# Virtual Network 생성
resource "azurerm_virtual_network" "vnet" {
  name                = "yjVnet"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

# Subnet 생성
resource "azurerm_subnet" "subnet" {
  name                 = "yjSubnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.vnet.name
    address_prefixes     = ["10.0.1.0/24"]
}

# Network Security Group
resource "azurerm_network_security_group" "nsg" {
  name                = "yjNSG"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

# NSG inbound rule(SSH port 22)
resource "azurerm_network_security_rule" "ssh" {
  name                        = "SSH"
  priority                    = 1001
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "22"
  source_address_prefix       = "*"
  destination_address_prefix  = "*"
  network_security_group_name = azurerm_network_security_group.nsg.name
  resource_group_name         = azurerm_resource_group.rg.name
}

# Public IP (20 count create)
resource "azurerm_public_ip" "pip" {
  count               = var.vm_cnt
  name                = format("yjPublicIP%02d", count.index + 1)
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  allocation_method   = "Static"
  sku                 = "Standard"
}

# Network Interface (20 count create)
resource "azurerm_network_interface" "nic" {
  count               = var.vm_cnt
  name                = format("yjNIC%02d", count.index + 1)
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  ip_configuration {
    name                          = "yjNICConfiguration"
    subnet_id                     = azurerm_subnet.subnet.id
    private_ip_address_allocation = "Dynamic"
    
    # public IP Connect
    public_ip_address_id = azurerm_public_ip.pip[count.index].id
  }
}

# NSG NIC (20 count create)
resource "azurerm_network_interface_security_group_association" "nic_nsg" {
  count                  = var.vm_cnt
  network_interface_id    = azurerm_network_interface.nic[count.index].id
  network_security_group_id = azurerm_network_security_group.nsg.id
}

# VM (20 count create)
resource "azurerm_virtual_machine" "vm" {
  count                 = var.vm_cnt
  name                  = format("yjVM%02d", count.index + 1)
  location              = azurerm_resource_group.rg.location
  resource_group_name   = azurerm_resource_group.rg.name
  network_interface_ids = [azurerm_network_interface.nic[count.index].id]
  # vm_size               = "Standard_DS1_v2"
  # vm_size               = "Standard_DS2_v3"
  vm_size               = "Standard_B2s"
  

  # Ubuntu Image 사용
  storage_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts-gen2"
    version   = "latest"
  }

  # OS 디스크 설정
  storage_os_disk {
    name              = format("yjOSDisk%02d", count.index + 1)
    caching           = "ReadWrite"
    create_option     = "FromImage"
    managed_disk_type = "Standard_LRS"
  }

  # 관리자 계정 설정
  os_profile {
    computer_name  = format("yjVM%02d", count.index + 1)
    admin_username = var.admin_username
    admin_password = var.admin_password
  }

  # SSH 설정
  os_profile_linux_config {
    disable_password_authentication = false
  }
  
  # upload file to VM
  provisioner "file" {
    source      = "../install_tools.sh"
    destination = "/tmp/install_tools.sh"

    connection {
      type     = "ssh"
      user     = var.admin_username
      password = var.admin_password
      host     = azurerm_public_ip.pip[count.index].ip_address
      port     = 22
    }
  }

  # exec uploaded script
  provisioner "remote-exec" {
    inline = [
      "chmod +x /tmp/install_tools.sh",
      "sudo /tmp/install_tools.sh"
    ]

    connection {
      type     = "ssh"
      user     = var.admin_username
      password = var.admin_password
      host     = azurerm_public_ip.pip[count.index].ip_address
      port     = 22
    }
  }

  # 퍼블릭 IP 의존성
  depends_on = [azurerm_public_ip.pip]
}

# Output: Public IP 목록 출력
output "public_ip_addresses" {
  value = azurerm_public_ip.pip[*].ip_address
}
```





## 3) **Terraform 단계별 실행**



### (1) azure env 설정

```sh

export ARM_CLIENT_ID="<APPID_VALUE>"
export ARM_CLIENT_SECRET="<PASSWORD_VALUE>"
export ARM_SUBSCRIPTION_ID="<SUBSCRIPTION_ID>"
export ARM_TENANT_ID="<TENANT_VALUE>"

export TF_VAR_admin_username="<vm_admin_username>"
export TF_VAR_admin_password="<vm_admin_password>"

===============================

# 20241020
export ARM_CLIENT_ID="37b7bc6f-..."
export ARM_CLIENT_SECRET="yap8Q~41~..."
export ARM_SUBSCRIPTION_ID="1d6c45e0-..."
export ARM_TENANT_ID="a3870566-..."

export TF_VAR_admin_username="song"
export TF_VAR_admin_password="Songpass123\!"


```



### (2) 실행

```bash

terraform init

terraform validate

terraform plan

terraform apply
 

terraform show

```



### (3) trouble shooting

```sh

# 유효한 image 목록 확인
$ az vm image list --output table

```





### (4) Clean up

```sh
# 리소스 삭제
$ terraform destroy

```













